/*
 * For more details take a look at the Java Quickstart chapter in the Gradle
 * user guide available at https://docs.gradle.org/4.10/userguide/tutorial_java_projects.html
 */
plugins {
    // Apply the java plugin to add support for Java
    id 'java'
    // For sorting out Eclipse output directories
    id 'eclipse'
    // Apply the application plugin to add support for building an application
    id 'application'
}

description = "Workshop Automation: Workshop Arm: Spring selection service"
group = 'wa.arm.springselector'
version = '1.0-SNAPSHOT'
mainClassName = 'wa.arm.springselector.SpringSelector'

sourceCompatibility = 1.8
targetCompatibility = 1.8

// By default, src/main and src/test are set up correctly
sourceSets {
    // Add integration tests
    intTest {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

// Adding additional configuration for integration testing
configurations {
    intTestImplementation.extendsFrom implementation
    intTestRuntimeOnly.extendsFrom runtimeOnly
}

// Project dependencies
dependencies {
    // This dependency is found on compile classpath of this component and consumers.
    // compile 'com.google.guava:guava:23.0'
	// https://mvnrepository.com/artifact/org.processing/core
	//compile group: 'org.processing', name: 'core', version: '3.3.7'
	// https://mvnrepository.com/artifact/org.hsqldb/hsqldb
    
    // Database dependencies
    compile "org.hsqldb:hsqldb:2.4.1"
    compile "org.hsqldb:hsqldb:2.4.1:sources"
    compile "org.hsqldb:hsqldb:2.4.1:javadoc"
    // Web Services dependencies
    compile "org.glassfish.jersey.containers:jersey-container-grizzly2-http:2.27"
    compile "org.glassfish.jersey.inject:jersey-hk2:2.27"
    compile "org.glassfish.jersey.media:jersey-media-json-binding:2.27"
    
    // Unit testing dependencies
    testCompile 'junit:junit:4.12'
    // Integration testing dependencies
    intTestImplementation 'junit:junit:4.12'
}

// Library repositories
repositories {
    jcenter()
    maven { url "http://repo.maven.apache.org/maven2" }
}

// Integration test task
task integrationTest(type: Test) {
    description = 'Runs integration tests.'
    group = 'verification'

    testClassesDirs = sourceSets.intTest.output.classesDirs
    classpath = sourceSets.intTest.runtimeClasspath
    shouldRunAfter test
}

check.dependsOn integrationTest
